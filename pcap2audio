#!/bin/sh

#
# pcap to audio file converter
#
# https://github.com/sp4rkie/pcap2audio
#

cat > $0_$$.awk << !

func setup( \
    )
{
    CLARGS = "$0|$1|$2|$3|$4|$5|$6|$7|$8|$9|${10}|${11}|${12}|${13}|${14}|${15}|${16}|${17}|${18}|${19}|${20}"

    # avoid dangling include files. incore inode still exists
    system("rm $0_$$.awk")

    # take care of shell expansion (if security is an issue)
    setup_lib($#, "awk|" CLARGS, $$, "`pwd`", "`cat /etc/hostname`", "$R_")
    PRP(CLARGS "\n")
}

func single_track(srcp, dstp, fname, \
    CMD, CMD1, line)
{
    # ignore things like tshark: The file ... appears to have been cut short in the middle of a packet.
    CMD = "tshark -n -r " pcap_file " -Y \"udp.srcport == " srcp " && udp.dstport == " dstp "\" -T fields -e rtp.payload" " 2> /dev/null"
    CMD1 = "xxd -r -p | sox -t al -r 8000 -c 1 - " fname
PR("CMD: " CMD)
PR("CMD: " CMD1)
    while (CMD | getline line > 0) {
        print gensub(":", "", "g", line) | CMD1
    }
    close(CMD)
    close(CMD1)
}

func pcap2audio( \
    cnt, CMD, line, i, a)
{
    CMD = "tshark -n -r " pcap_file " -Y rtp -T fields -e _ws.col.Time -e udp.srcport -e udp.dstport"
PR("calls:")
    while ((line = ex_line(CMD)) != -1) {
        if (split(line, a) != 4 \
         || !match(line, "^[0-9:.\t -]+$")) {
            if (line) PR(line)
            continue
#            exit_(line)
        }
        if (FORW[a[4] " " a[3]]) {
            if (!BACK[a[4] " " a[3]]) {
                BACKT[a[4] " " a[3]] = a[1] " " a[2]
PR("reverse: " a[1] " " a[2] " " a[3] " " a[4])
            }
            ++BACK[a[4] " " a[3]]
        } else {
            if (!FORW[a[3] " " a[4]]) {
                FORWT[a[3] " " a[4]] = a[1] " " a[2]
                FORWS[cnt++] = a[3] " " a[4]
PRF("forward: " a[1] " " a[2] " " a[3] " " a[4] " ")
            }
            ++FORW[a[3] " " a[4]]
        }
    }
PR("\nextracting audio:")
    for (i = 0; i < cnt; ++i) {
        PR("\n" i " -> " FORWS[i] " " FORWT[FORWS[i]] " [ " FORW[FORWS[i]] " ] " BACKT[FORWS[i]] " [ " BACK[FORWS[i]] " ]")
        split(FORWS[i], a)
        fname_pfx = "/tmp/" PRGBNAME "_" gensub(" ", "_", "g", FORWT[FORWS[i]])
        fname_pfx = gensub("\\\.[^.]+$", "", "g", fname_pfx)
PR("forward direction:")
        single_track(a[1], a[2], fname_pfx "_f.wav")
        # generate the reversed direction too if applicable
        if (BACK[a[1] " " a[2]]) {
PR("reverse direction:")
            single_track(a[2], a[1], fname_pfx "_r.wav")
PR("merge forward reverse:")
            CMD = "sox --combine merge " fname_pfx "_f.wav" " " fname_pfx "_r.wav" " " fname_pfx "_m.wav"
            ex(CMD, 3)
            ex("rm " fname_pfx "_f.wav" " " fname_pfx "_r.wav", 3)
        } else {
PR("reverse direction not avail")
        }
    }
}

func usage( \
    locked, line, a)
{
    PR("Usage: " PRGBNAME)
    while ((line = ex_line("cat " PRGNAME)) != -1) {
        if (locked) {
            if (match(line, "match[^,]+, \"(.+)\"[^\"]+# (.*)$", a)) {
                PR(sprintf("  %-21s - %s", gensub("\\\\$", "", "g", a[1]), a[2]))
            }
        } else {
            if (match(line, "^func process_cmdline")) ++locked
        }
    }
}

func process_cmdline( \
    i, a)
{
    #
    # regexps for arg matching are kept somewhat sloppy to avoid clutter in usage() text output
    #
    for (i = 2; i < _ARGC; ++i) {
        if (match(_ARGV[i] _ARGV[i + 1], "-h$")) { # print this help and exit
            usage()
            exit
        } else if (match(_ARGV[i] " " _ARGV[i + 1], "-f (.+)$")) { # pcap input file
            pcap_file = _ARGV[i + 1]
            ++i
        } else if (match(_ARGV[i], "-v$")) { # increase logging verbosity
            ++verbose
        } else {
            usage()
            ++ERR; exit
        }
    }
    if (!pcap_file) {
        exit_("no pcap input file given")
    }
    pcap2audio()
}

BEGIN {
    QUIET = 1
    setup()
    process_cmdline()
    exit
}

END { 
    cleanup()
    PRP("\n" PRGBNAME " " "exits with: " (ERR ? ERR : 0) " " "error(s)")
    PRP("====== [" sprintf(" %5d ", PROCINFO["pid"]) "] program stop  [" strftime() "] on " HOSTNAME " ======")
    close(PROTOCOL)
    exit ERR
}

!

[ -t 0 ] && STDIN="< /dev/null"
eval exec awk -f $0.awklib -f $0_$$.awk $STDIN 2>&1

